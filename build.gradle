description '微服务的父工程'
// 构建 Gradle 脚本自身需要的资源，可以声明的资源包括依赖项、第三方插件、maven 仓库地址等。
buildscript {
    ext {
        springBootVersion = '2.3.12.RELEASE'
        springCloudVersion = 'Hoxton.SR12'
        springCloudAlibabaVersion = '2.2.8.RELEASE'
    }
    //设置仓库
    repositories {
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        maven { url 'https://maven.aliyun.com/repository/spring-plugin' }
    }
    // 依赖
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.freefair.gradle:lombok-plugin:6.5.0.3"
    }
}

// 全局项目配置
allprojects {
    group 'com.github'
    version '1.0'

    // 设置编码信息
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
    tasks.withType(Javadoc) {
        options.encoding = "UTF-8"
    }
    // 设置仓库
    repositories {
        maven { url 'https://maven.aliyun.com/repository/public' }
        mavenCentral()
    }
}

apply from: 'version.gradle'

// 配置所有的子模块
subprojects {
    // 添加插件
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "io.freefair.lombok"
    // 设置 JDK
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    // 设置编码信息
    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"
    // 公用的依赖
    dependencies {
        testImplementation(platform('org.junit:junit-bom:5.7.2'))
        testImplementation('org.junit.jupiter:junit-jupiter')
    }
    test {
        useJUnitPlatform()
    }
    // 相当于 Maven 的 <DependencyManagement> 标签
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }
        dependencies {
            for (depJar in rootProject.ext.dependencies) {
                dependency(depJar)
            }
        }
    }
}

// 配置 ones-common
project(':ones-common') {
    description '公共模块'
    // 依赖
    dependencies {
        api 'cn.hutool:hutool-all:5.8.4'
    }
}


// 配置 ones-service
project(':ones-service') {
    description '微服务模块'

    subprojects {
        apply plugin: 'org.springframework.boot'

        dependencies {
            api project(':ones-common')
            /* mybatis-plus */
            api 'com.baomidou:mybatis-plus-boot-starter'
            /* 自动建表 */
            api 'io.gitee.zero-wsh:acTable:2.0.6'
            /* 邮件 */
            api 'org.springframework.boot:spring-boot-starter-mail'
            /* 安全 */
            api 'org.springframework.boot:spring-boot-starter-security'
            /* JSR 303 */
            api 'org.springframework.boot:spring-boot-starter-validation'
            /* web */
            api 'org.springframework.boot:spring-boot-starter-web'
            /* devtools */
            developmentOnly 'org.springframework.boot:spring-boot-devtools'
            /* MySQL 驱动 */
            api 'mysql:mysql-connector-java'
            /* 配置文件提示 */
            api 'org.springframework.boot:spring-boot-configuration-processor'
            /* 单元测试 */
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            /* mapstruct */
//            api 'org.mapstruct.extensions.spring:mapstruct-spring-extensions:0.1.2'
            /* 数据库连接池 */
            api 'com.alibaba:druid-spring-boot-starter:1.2.5'
        }
    }
}

